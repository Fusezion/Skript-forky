test "ExprTool":
	spawn a skeleton at test location:
		set helmet of entity to diamond helmet # preventing death from sun
		set {_skeleton} to entity

	# Main Hand

	set main hand of {_skeleton} to a diamond
	assert main hand of {_skeleton} is a diamond with "The held item of the skeleton wasn't set to a diamond"

	add 3 diamonds to the tool of {_skeleton}
	assert weapon of {_skeleton} is 4 diamonds with "The held item of the skeleton didn't become 4 diamonds"

	remove stone from the held item of {_skeleton}
	assert held item of {_skeleton} is 4 diamonds with "The held item of the skeleton was no longer 4 diamonds after removing a different item"

	remove 2 diamonds from the main hand of {_skeleton}
	assert main hand of {_skeleton} is 2 diamonds with "The held item of the skeleton did not become 2 diamonds after removing 2 diamonds from 4"

	clear main hand of {_skeleton}
	assert main hand of {_skeleton} is air with "The held item of the skeleton did not become air after being cleared"

	# Off Hand

	set offhand of {_skeleton} to a diamond
	assert offhand of {_skeleton} is a diamond with "The offhand item of the skeleton wasn't set to a diamond"

	add 3 diamonds to the offhand tool of {_skeleton}
	assert offhand tool of {_skeleton} is 4 diamonds with "The offhand item of the skeleton didn't become 4 diamonds"

	remove stone from the offhand item of {_skeleton}
	assert offhand of {_skeleton} is 4 diamonds with "The offhand item of the skeleton was no longer 4 diamonds after removing an incorrect item"

	remove 2 diamonds from the offhand of {_skeleton}
	assert offhand of {_skeleton} is 2 diamonds with "The offhand item of the skeleton did not become 2 diamonds after removing 2 diamonds from 4"

	clear offhand of {_skeleton}
	assert offhand of {_skeleton} is air with "The offhand item of the skeleton did not become air after being cleared"

	# Cleanup
	delete entity within {_skeleton}
